import com.github.mgk.gradle.*

buildscript {
    dependencies {
        classpath group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
        classpath group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
        classpath group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'
        classpath group: 'javax.activation', name: 'activation', version: '1.1.1'
    }
}

plugins {
    id "java"
    id "checkstyle"
    id "co.riiid.gradle" version "0.4.2"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

println "Host: " + java.net.InetAddress.getLocalHost()
println "Gradle: " + gradle.gradleVersion + " JVM: " + org.gradle.internal.jvm.Jvm.current() + " Groovy: " + GroovySystem.getVersion()
println "Build: group: '${project.group}', name: '${project.name}', version: '${project.version}'"
println "Timestamp: " + java.time.Instant.now().atZone(java.time.ZoneId.systemDefault()).toString()

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    versions = [
        "elasticsearch": version.replaceAll(/\.[0-9]+(|-SNAPSHOT)$/, ""),
        "gson": "2.3.1",
        "log4j": "2.6.2",
        "junit": "4.12"
    ]
}

configurations {
    releaseJars {
        extendsFrom runtime
        exclude group: "org.elasticsearch"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "org.apache.logging.log4j"
    }
}

dependencies {
    compile "org.elasticsearch:elasticsearch:${versions.elasticsearch}"
    compile "com.google.code.gson:gson:${versions.gson}"
    compile "org.apache.logging.log4j:log4j-api:${versions.log4j}"
    testCompile "junit:junit:${versions.junit}"
    releaseJars "${project.group}:${project.name}:${project.version}"
}

compileJava {
    options.encoding = "UTF-8"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked,deprecation"
}

task javadocJar(type: Jar, dependsOn: classes) {
    from javadoc
    into "build/tmp"
    classifier "javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    into "build/tmp/sources"
    classifier "sources"
}

task copyPluginFiles(type: Copy) {
    from "src/main/templates"
    into "build/tmp/plugin"
    expand([
        "descriptor": [
            "name"                : pluginName,
            "classname"           : pluginClassname,
            "description"         : pluginDescription,
            "version"             : project.property("version"),
            "javaVersion"         : project.property("targetCompatibility"),
            "elasticsearchVersion": versions.elasticsearch
        ]
    ])
    outputs.upToDateWhen { false }
}

task buildPluginZip(type: Zip, dependsOn: [":jar", "copyPluginFiles"]) {
    from configurations.releaseJars
    from "build/tmp/plugin"
}

artifacts {
    archives javadocJar, sourcesJar, buildPluginZip
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    toolVersion = "8.2"
}

def getGitCommitHash() {
    def gitFolderPath = "$projectDir/.git/"
    def gitFolder = new File(gitFolderPath + "HEAD");
    if (!gitFolder.exists()) {
        return null
    }

    def head = gitFolder.text.split(":")
    def isCommit = (head.length == 1)

    if (isCommit) {
        return head[0].trim()
    }

    def refHead = new File(gitFolderPath + head[1].trim())
    return refHead.text.trim()
}

github {
    owner = github_owner
    repo = github_repo
    token = github_token
    tagName = version
    targetCommitish = getGitCommitHash()
    name = version
    assets = [
        "build/distributions/${project.name}-${version}.zip"
    ]
}

githubRelease {
    dependsOn("buildPluginZip")
}

task release() {
    if (version.endsWith("-SNAPSHOT")) {
        doLast {
            println("SNAPSHOT: Nothing to release !")
        }
    } else {
        dependsOn(["githubRelease"])
    }
}
